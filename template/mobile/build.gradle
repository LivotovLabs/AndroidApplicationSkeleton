buildscript {
    repositories {
        jcenter()
    }

    dependencies {
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId appId
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode appVersionMajor * 10000 + appVersionMinor * 1000 + appVersionPatch * 100 + appVersionBuild
        versionName "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}, build ${gitSha()}"
        multiDexEnabled true
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
        }

        debug {
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            debuggable true
            shrinkResources false
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        development {
        }

        production {
        }
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'doc/HOW_TO.TXT'
        exclude 'doc/README'
        exclude '.readme'
    }
}

dependencies {
    /**
     * Local libs and modules
     */
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')

    /**
     * ===============================================================
     * System Libraries
     * ===============================================================
     */

    /**
     * Core libs
     */
    compile miscLibs.multidex
    compile miscLibs.rx
    compile miscLibs.eventbus
    compile miscLibs.logging
    compile miscLibs.utils

    /**
     * Annotation processing libs
     */
    compile aptLibs.java
    apt aptLibs.apt

    /**
     * Network and REST API
     */
    compile networkLibs.rest
    compile networkLibs.imaging

    /**
     * Geolocation
     */
    compile geoLibs

    /**
     * Support library
     */
    compile supportLibs.appCompat
    compile supportLibs.annotation
    compile supportLibs.palette
    compile supportLibs.recyclerView
    compile supportLibs.design
    compile supportLibs.cardView

    /**
     * Play Services
     */
    compile playServicesLibs.base
    compile playServicesLibs.wearable
    compile playServicesLibs.location
    compile playServicesLibs.analytics

    /**
     * ===============================================================
     * Third-party base libs
     * ===============================================================
     */

    /**
     * MVP
     */
    compile 'eu.inloop:androidviewmodel:1.0.0'

    /**
     * Material design support
     */
    compile "com.mikepenz:materialize:0.8.8@aar"
    compile('com.mikepenz:materialdrawer:5.2.6@aar') { transitive = true }
    compile 'com.roughike:bottom-bar:1.3.3'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.6@aar') { transitive = true }


    /**
     * UI libs
     */
    compile 'com.wdullaer:materialdatetimepicker:2.3.0'
    compile 'com.kbeanie:multipicker:1.0.10@aar'
    compile('com.mikepenz:fastadapter:1.4.4@aar') { transitive = true }
    compile 'com.mikepenz:fastadapter-extensions:1.4.2@aar'
    compile "com.daimajia.swipelayout:library:1.2.0@aar"

    /**
     * ===============================================================
     * Tests
     * ===============================================================
     */
    testCompile 'junit:junit:4.12'
}

apply from: "${rootProject.projectDir}/config/signing.gradle"
