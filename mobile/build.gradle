apply plugin: 'com.android.application'

repositories {
    maven {
        url "http://dl.bintray.com/livotovlabs/maven"
    }
}

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId appId
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode appVersionMajor * 10000 + appVersionMinor * 1000 + appVersionPatch * 100 + appVersionBuild
        versionName "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}, build ${gitSha()}"
        multiDexEnabled true
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
        }

        debug {
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            debuggable true
            shrinkResources false
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        development {
        }

        production {
        }
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'doc/HOW_TO.TXT'
        exclude 'doc/README'
        exclude '.readme'
    }
}

dependencies {
    /**
     * Local libs and modules
     */
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')

    /**
     * System android tools
     */
    compile miscLibs.multidex

    /**
     * Support library
     */
    compile supportLibs.appCompat
    compile supportLibs.annotation
    compile supportLibs.palette
    compile supportLibs.recyclerView
    compile supportLibs.design
    compile supportLibs.cardView

    /**
     * Play Services
     */
    compile playServicesLibs.base
    compile playServicesLibs.wearable
    compile playServicesLibs.location
    compile playServicesLibs.analytics

    /**
     * User libs
     */
    compile 'org.greenrobot:eventbus:3.0.0'

    /**
     * Tests
     */
    testCompile 'junit:junit:4.12'
}

apply from: "${rootProject.projectDir}/config/signing.gradle"
